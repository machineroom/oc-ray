
PROC controlSystem ( []CHAN OF ANY toWorkers, []CHAN OF ANY fromWorkers, CHAN OF ANY fb.in, CHAN OF INT image.selection, VAL INT machines )

  #INCLUDE "PROTOCOL.INC"

  [4]INT free.workers:

  PROC writes ( VAL [] BYTE s, [256] INT buffer )
    SEQ
      SEQ i = 0 FOR SIZE s
        buffer [i] := INT s [i]
      fb.in ! c.message; SIZE s; [ buffer FROM 0 FOR SIZE s ]
  :

  #INCLUDE "WORLD.INC"

  PROC sendPatches ( CHAN OF ANY out, VAL INT patchEdge )
    --VAL patchesWide IS 256 / patchEdge :
    VAL patchesWide IS 60 / patchEdge :
    SEQ
      SEQ x = 0 FOR patchesWide
        SEQ y = 0 FOR patchesWide
          out ! c.render; (x * patchEdge) + 128; (y * patchEdge) + 128
      out ! c.stop
  :

  PROC loadBalance ( CHAN OF ANY in, out, return, fb.in,
                     VAL INT machines,
                     [256] INT buffer )
    BOOL passActive, farmActive :
    INT free, command :
    SEQ
      farmActive := TRUE
      passActive := TRUE
      -- calculate total workers
      free := 0
      SEQ w=0 FOR SIZE free.workers
        SEQ
          free := free + free.workers[w]
      WHILE passActive OR farmActive
        PRI ALT
          (free > 0) & in ? command
            IF
              command = c.render
                INT x, y :
                SEQ
                  --writes ( "*N*C S c.render to system", buffer)
                  in  ? x; y
                  out ! command; x; y
                  free := free - 1
              command = c.stop
                SEQ
                  writes ( "*N*C R c.stop from pumper", buffer)
                  out ! c.stop
                  passActive := FALSE
          return ? command
            IF
              command = c.patch
                INT x, y, size :
                SEQ
                  --writes ( "*N*C R patch", buffer)
                  return   ? x; y; size
                  fb.in ! command; x; y; size
                  SEQ i = 0 FOR size
                    SEQ
                      return   ? [ buffer FROM 0 FOR size ]
                      fb.in ! [ buffer FROM 0 FOR size ]
                  free := free + 1
              command = c.message
                INT size :
                SEQ
                  return   ? size; [ buffer FROM 0 FOR size ]
                  fb.in ! command; size; [ buffer FROM 0 FOR size ]
              command = c.stop
                SEQ
                  farmActive := FALSE
                  writes ( "*N*C received c.stop", buffer)
      fb.in ! c.stop
  :

  -- send render to available worker
  PROC outputBuffer ( CHAN OF ANY in, []CHAN OF ANY out )
    INT command :
    SEQ
      in ? command
      WHILE command <> c.stop
        SEQ
          IF
            command = c.render
              INT x, y :
              SEQ
                in  ? x; y
                IF w=0 FOR SIZE out
                  free.workers[w]>0 
                    out[w] ! command;x;y
                --out ! command; x; y
          in ? command
      --out ! command
      SEQ i=0 FOR SIZE out
        out[i] ! command
  :

  -- receive patches from one of workers
  PROC returnBuffer ( []CHAN OF ANY fromWorkers, CHAN OF ANY out, [256] INT buffer )
    INT command :
    SEQ
      command := c.invalid
      WHILE command <> c.stop
        ALT w=0 FOR SIZE fromWorkers
          fromWorkers[w] ? command
            IF
              command = c.patch
                INT x, y, size :
                SEQ
                  fromWorkers[w]  ? x; y; size
                  out ! command; x; y; size
                  SEQ i = 0 FOR size
                    SEQ
                      fromWorkers[w]  ? [ buffer FROM 0 FOR size ]
                      out ! [ buffer FROM 0 FOR size ]
              command = c.message
                INT size :
                SEQ
                  fromWorkers[w] ? size; [ buffer FROM 0 FOR size ]
                  out ! command; size; [ buffer FROM 0 FOR size ]
  :
  
  -- send map to workers
  PROC sendMap ([]CHAN OF ANY toWorkers)
    SEQ w = 0 FOR SIZE toWorkers
      SEQ
        toWorkers[w] ! c.map
  :

  -- receive mapAck response from all workers
  PROC receiveMapAck ([]CHAN OF ANY fromWorkers)
    INT command:
    [256] INT buffer:
    INT count:
    INT done:
    INT expected:
    BOOL continue:
    SEQ
      done := 0
      expected := SIZE fromWorkers
      continue := TRUE
      WHILE continue
        ALT w=0 FOR SIZE fromWorkers
          fromWorkers[w] ? command
            IF
              command = c.mapAck
                SEQ
                  done := done + 1
                  fromWorkers[w] ? count
                  free.workers[w] := count
                  IF
                    done = expected
                      continue := FALSE
                    TRUE
                      SKIP
  :

  -- fb.in is patches to framebuffer
  --PROC controlSystem ( []CHAN OF ANY toWorkers, []CHAN OF ANY fromWorkers, CHAN OF ANY fb.in, CHAN OF INT image.selection, VAL INT machines )
  VAL patchEdge IS 3 :
  [3][256] INT buffer :
  CHAN OF ANY render.commands, to.farm, from.farm :
  INT model:
  SEQ
    SEQ i=0 FOR SIZE free.workers
      free.workers[i] := 0
    WHILE TRUE
      SEQ
        image.selection ? model
        -- map the size of the network (how many workers down each link)
        sendMap (toWorkers)
        receiveMapAck (fromWorkers)
        SEQ i=0 FOR SIZE toWorkers
          pumpWorldModels ( toWorkers[i], model, patchEdge, buffer [0] )
        PAR
          sendPatches  ( render.commands, patchEdge )         -- sends c.render,x,y for all patches
          outputBuffer ( to.farm, toWorkers )               -- sends commands to farm
          returnBuffer ( fromWorkers, from.farm, buffer [1] )  -- receives patches from farm
          loadBalance  ( render.commands, to.farm, from.farm, fb.in, machines, buffer [2] )
:

