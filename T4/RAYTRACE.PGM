--{{{  kilo, mega 
VAL K IS 1024:
VAL M IS K*K:
--}}}

--{{{  HIGH, LOW priority levels
VAL HIGH IS 0:
VAL LOW IS 1:
--}}}

ARC Hostlink:

-- network.inc is written by ispy_to_network.py
#INCLUDE "network.inc"

--{{{  Mapping
NODE framebuf.p :
NODE cntlsys.p :
[workers]NODE raytrace.p :

MAPPING
  DO
    MAP framebuf.p ONTO System[0]
    MAP cntlsys.p ONTO System[1]
    DO i = 0 FOR workers
      MAP raytrace.p[i] ONTO System[i+2]
:
--}}}

--{{{  Code
#INCLUDE "hostio.inc"
#USE "framebuf.c4h"
#USE "cntlsys.c4h"
#USE "raytrace.c4h"

CONFIG
  CHAN OF SP frame.in, frame.out :
  PLACE frame.in, frame.out ON Hostlink :
  [workers]CHAN OF ANY forward, back :
  CHAN OF ANY graphics, soft :

  VAL true      IS #FFFF :
  VAL false     IS #0000 :

  PAR
    PROCESSOR framebuf.p
      frameBuffer (frame.in, frame.out, graphics)
    PROCESSOR cntlsys.p
      controlSystem (forward[0], back[0], graphics, workers)
    PAR i = 0 FOR workers-1
      PROCESSOR raytrace.p[i]
        rayTrace (forward[i], back[i], back[i+1], forward[i+1], true, i)
    VAL i IS workers - 1 :
    PROCESSOR raytrace.p[i]
      rayTrace (forward [i], back[i], soft, soft, false, i)

:
--}}}
  


