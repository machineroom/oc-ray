#INCLUDE "hostio.inc"
#INCLUDE "streamio.inc"

#USE "hostio.lib"
#USE "streamio.lib"

-- fs, ts are host link
-- in is from cntlsys
PROC frameBuffer (CHAN OF SP fs, ts, CHAN OF ANY in)

  VAL c.stop    IS 0 :
  VAL c.render  IS 1 :  -- x0; y0
  VAL c.object  IS 2 :  -- size; [ size ]
  VAL c.light   IS 3 :  -- size; [ size ]
  VAL c.patch   IS 4 :  -- worker; x; y; patchSize; [ patchSize][patchSize]
  VAL c.runData IS 5 :  -- ambient light, rendering mode, screen pos etc.
  VAL c.message IS 6 :  -- size; [ words ]
  VAL c.done    IS 7 :  -- worker;


  ---------------------------- COMMANDS PROTOCOL -----------------------------

  INT command :
  INT32 Status :
  SEQ
    so.write.string.nl (fs, ts, " The inmos ray tracer is GO")
    in ? command
    WHILE command <> c.stop
      SEQ
        IF
          command = c.patch
            INT x, y, size, worker :
            SEQ
              in ? worker; x; y; size
              so.write.string (fs, ts, "FB patch from worker ")
              so.write.int (fs, ts, worker, 1)
              so.write.string.nl(fs, ts, "")
          command = c.done
            INT worker :
            SEQ
              in ? worker
              so.write.string (fs, ts, "Worker done: ")
              so.write.int (fs,ts, worker, 1)
              so.write.string.nl(fs, ts, "")
          TRUE
            SEQ
              so.write.string.nl (fs, ts,"FB GARBAGE COMMAND")
              STOP
        in ? command
    SEQ
      so.write.string (fs, ts, " DONE!!!! ")
      so.write.nl (fs, ts)
      so.exit (fs, ts, Status)      
:

