-- out, back is link to first worker
-- graphics is output to framebuf
-- machines is total number of workers

-- upstream commands:
--    c.render
--    c.stop
-- downstream command:
--    c.patch
--    c.done
--    c.stop

PROC controlSystem ( CHAN OF ANY out, back, graphics, VAL INT machines )


  VAL c.stop        IS 0 :
  VAL c.render      IS 1 :  -- x0; y0
  VAL c.patch       IS 4 :  -- worker; x; y; patchSize; [ patchSize][patchSize]
  VAL c.done        IS 7 :  -- worker
  VAL c.tick        IS 8 :  -- worker;debug;


  PROC sendPatches ( CHAN OF ANY out, VAL INT patchEdge)
    VAL patchesWide IS 256 / patchEdge :
    SEQ
      SEQ x = 0 FOR patchesWide
        SEQ y = 0 FOR patchesWide
          SEQ
            out ! c.render; (x * patchEdge) + 128; (y * patchEdge) + 128
      out ! c.stop
  :

  PROC loadBalance ( CHAN OF ANY in, out, return, graphics,
                     VAL INT machines)
    BOOL passActive, farmActive :
    INT free, command :
    SEQ
      farmActive := TRUE
      passActive := TRUE
      free := machines 
      WHILE passActive OR farmActive
        SEQ
          PRI ALT
            (free > 0) & in ? command
              SEQ
                IF
                  command = c.render
                    INT x, y :
                    SEQ
                      in  ? x; y
                      out ! command; x; y
                      free := free - 1
                  command = c.stop
                    SEQ
                      out ! c.stop
                      passActive := FALSE
            return ? command
              SEQ
                IF
                  command = c.patch
                    INT worker, x, y, size :
                    SEQ
                      return   ? worker; x; y; size
                      graphics ! command; worker; x; y; size
                      free := free + 1
                  command = c.done
                    INT worker :
                    SEQ
                      return   ? worker
                      graphics ! command; worker
                  command = c.tick
                    INT p1,p2 :
                    SEQ
                      return   ? p1;p2
                      graphics ! command; p1;p2
                  command = c.stop
                    SEQ
                      farmActive := FALSE
      graphics ! c.stop
  :

  -- send messages downstream (towards 1st worker)
  PROC outputBuffer ( CHAN OF ANY in, out )
    INT command :
    SEQ
      in ? command
      WHILE command <> c.stop
        SEQ
          IF
            command = c.render
              INT x, y :
              SEQ
                in  ? x; y
                out ! command; x; y
          in ? command
      out ! command
  :

  -- send messages upstream (from 1st worker, to framebuf)
  PROC returnBuffer ( CHAN OF ANY in, out )
    INT command :
    SEQ
      in ? command
      WHILE command <> c.stop
        SEQ
          IF
            command = c.patch
              INT x, y, size, worker :
              SEQ
                in  ? worker; x; y; size
                out ! command; worker; x; y; size
            command = c.done
              INT worker :
              SEQ
                in ? worker
                out ! command; worker
            command = c.tick
              INT p1,p2 :
              SEQ
                in   ? p1;p2
                out ! command; p1; p2
          in ? command
      out ! command
  :

  VAL patchEdge IS 8 :
  CHAN OF ANY a, b, c :
  WHILE TRUE
    SEQ
      PAR
        sendPatches  ( a, patchEdge)
        outputBuffer ( b, out  )
        returnBuffer ( back, c )
        loadBalance  ( a, b, c, graphics, machines )
:

